print "ПУЗЫРЬКОВАЯ СОРТИРОВКА\n"

def bubbleSort(arr) {
    n = len(arr)
    for i = 0; i < n-1; i = i+1 {
        for j = 0; j < n-i-1; j = j+1 {
            if arr[j] > arr[j+1] {
                temp = arr[j]
                arr[j] = arr[j+1]
                arr[j+1] = temp
            }
        }
    }
}

array = [64, 34, 25, 12, 22, 11, 90]
bubbleSort(array)
print "Сортированный массив: " + array + "\n\n-------------------------------\n"

print "ВОЗВЕДЕНИЕ В СТЕПЕНЬ\n"

def pow(base, exponent) {
    result = base
    for i = 1; i < exponent; i = i + 1 {
        result = result * base
    }
    return result
}

print "2^10 = " + pow(2, 10)
print "\n\n-------------------------------\n"

print "ФИБОНАЧИ\n"

def fibonacci(n) {
    if n <= 1 {
        return n
    }
    a = 0
    b = 1
    for i = 2; i <= n; i = i+1 {
        c = a + b
        a = b
        b = c
    }
    return b
}

print "Fibonacci(10): " + fibonacci(10)


print "\n\n-------------------------------\n"

print "АЛГОРИТМ СЖАТИЯ\n"

def rleEncode(input) {
    encoded = ""
    count = 1
    for i = 1; i < length(input); i = i+1 {
        if input[i] == input[i-1] {
            count = count + 1
        } else {
            encoded = encoded + input[i-1] + count
            count = 1
        }
    }
    encoded = encoded + input[length(input)-1] + count
    return encoded
}

print rleEncode("AAAABBBCCDAA")



print "\n\n-------------------------------\n"

print "ПОИСК НАИБОЛЬШЕЙ ПОДСТРОКИ\n"

def longestCommonSubstring(s1, s2) {
    m = length(s1)
    n = length(s2)
    dp = newArray(m + 1, n + 1)
    maxLen = 0
    endIndex = 0

    for i = 1; i <= m; i = i + 1 {
        for j = 1; j <= n; j = j + 1 {
            if s1[i - 1] == s2[j - 1] {
                dp[i][j] = dp[i - 1][j - 1] + 1
                if dp[i][j] > maxLen {
                    maxLen = dp[i][j]
                    endIndex = i - 1
                }
            }
        }
    }


    result = newArray(maxLen)
    for i = 0; i < maxLen; i = i + 1 {
        result[i] = s1[endIndex - maxLen + 1 + i]
    }
    return result
}

print longestCommonSubstring("EHALGREKACHEREZREKU", "VIDITGREKAVREKERAK")



print "\n\n-------------------------------\n"

print "БИНАРНЫЙ ПОИСК\n"

def binarySearch(arr, x) {
    low = 0
    high = len(arr) - 1

    while low <= high {
        mid = round((low + high) / 2)
        if arr[mid] < x {
            low = mid + 1
        } else if arr[mid] > x {
            high = mid - 1
        } else {
            return mid
        }
    }
    return -1
}

sorted_array = [1, 3, 5, 7, 9]
print "Индекс числа 7: " + binarySearch(sorted_array, 7)



print "\n\n-------------------------------\n"

print "ФАКТОРИАЛ\n"

def factorial(n) {
    if n <= 1 {
        return 1
    }
    return n * factorial(n - 1)
}

print "6! = " + factorial(6)



print "\n\n-------------------------------\n"

print "БЫСТРАЯ СОРТИРОВКА\n"

def partition(arr, low, high) {
    pivot = arr[high]
    i = low - 1

    for j = low; j < high; j = j + 1 {
        if arr[j] < pivot {
            i = i + 1
            temp = arr[i]
            arr[i] = arr[j]
            arr[j] = temp
        }
    }


    temp = arr[i + 1]
    arr[i + 1] = arr[high]
    arr[high] = temp

    return i + 1
}

def quickSort(arr, low, high) {
    if low < high {
        pi = partition(arr, low, high)

        quickSort(arr, low, pi - 1)
        quickSort(arr, pi + 1, high)
    }
}


def qsort(arr) {
    quickSort(arr, 0, len(arr) - 1)
    return arr
}


array = [10, 7, 8, 9, 1, 5]
print "Несортированный массив: " + array + "\n"
sortedArray = qsort(array)
print "Отсортированный массив: " + sortedArray




